namespace com.ibm.streamsx.inet.sample;
use com.ibm.streamsx.inet.http::httpGet;
use com.ibm.streamsx.inet.http::httpPost;

composite GetPost {
graph
stream<rstring result> GetStream = Custom() {

logic onProcess: {

mutable int32 error = 0;
rstring result1 = httpGet("https://tgftp.nws.noaa.gov/data/observations/metar/cycles/00Z.TXT",
    (list<rstring>)[],"","",error);
if (error == 0) {
    printStringLn(result1);
    submit({result=result1},GetStream);
}
else {
    appTrc(Trace.error,"Error code was "+(rstring)error);
}
}

}

() as checkGet = Custom(GetStream) {
 
 logic state: {
    mutable int32 numTuples = 0;
 }
 
 onTuple GetStream:
    {
        if (result != "") {
            numTuples++;
        }

    }
    onPunct GetStream:
    {
        if (currentPunct() == Sys.FinalMarker) {
            if (numTuples != 1) {
                abort();
            }
        }
    }
}

() as sentTuples = Custom() {
logic onProcess: {
    block(5.0);
    mutable int32 error =0;
    mutable int32 i = 0;
    while (i < 5) {
    rstring toPost = "str=This is tuple%20"+(rstring)i+"&iter="+(rstring)i;
    appTrc(Trace.info,"To post: "+toPost);
    mutable list<rstring> replyHeaders = [];
    httpPost(toPost,"http://127.0.0.1:8080/Received/ports/output/0/inject",["ContentType: application/x-www-form-urlencoded"],"","",replyHeaders,error);
    if (error != 0) {
        appTrc(Trace.error,"Error code on post was "+(rstring)error);
    }
    i++;
    }
    } // end onProcess
}// end custom

}
